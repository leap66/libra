apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.leap.libra"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode gitVersionCode()
        versionName currentName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "SERVER_URL", System.getenv("SERVER_URL") ?: "\"http://172.17.6.39:8088/leap/\"")
        buildConfigField("String", "UPDATE_URL", System.getenv("UPDATE_URL") ?: "\"https://a.qianfan123.com/ap/s/\"")
        buildConfigField("String", "LOG_URL", System.getenv("LOG_URL") ?: "\"https://logstash-test.100sails.com:8905\"")
        buildConfigField("String", "LOG_URL_BURY", System.getenv("LOG_URL_BURY") ?: "\"https://track.qianfan12.com:9999/track/test/v1/qfapp\"")
        buildConfigField("String", "LOG_TOKEN", System.getenv("LOG_TOKEN") ?: "\"bG9nc3Rhc2g6YW9ESjBKVmdrZk5QamFybg==\"")
        buildConfigField("String", "LOG_TOKEN_BURY", System.getenv("LOG_TOKEN_BURY") ?: "\"bG9nc3Rhc2g6YW9ESjBKVmdrZk5QamFybg==\"")

        multiDexEnabled true

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }

        manifestPlaceholders = [
                app_name       : "@string/app_name",
                PUSH_APP_KEY   : System.getenv('PUSH_APP_KEY') ?: "24835114",
                PUSH_APP_SECRET: System.getenv('PUSH_APP_SECRET') ?: "7697a4b4fbf195d35e5922a34467e82a"
        ]
        println("------------------环境变量--------------")
        println("versionCode=" + gitVersionCode())
        println("versionName=" + gitVersionName())
        getBuildConfigFields().each { item ->
            println("KEY=" + item.key)
            println("VALUE=" + item.value.getValue())
        }
        println("------------------环境变量--------------")
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [app_name: "leap"]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        def versionCode = gitVersionCode()
        def versionName = variant.buildType.name + "." + versionCode
        if (variant.buildType.name == "release") {
            versionName = gitVersionName()
        }

        variant.outputs.all { output ->
            output.versionCodeOverride = versionCode
            output.versionNameOverride = versionName
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "libra-${variant.buildType.name}-${versionName}-${versionCode}.apk"
                outputFileName = fileName
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

static def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

static def gitVersionName() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()
    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern
    if (matcher) {
        version = version.substring(0, matcher.start())
    }
    return version
}

static def currentName() {
    def versionName = gitVersionName()
    if (versionName == null || versionName == "") {
        return "debug." + gitVersionCode()
    }
    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':grade')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support:multidex:1.0.1'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.1.1'
    implementation 'com.jungly:gridPasswordView:0.3'
}
